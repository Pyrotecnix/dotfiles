set nocompatible                            " Use Vim settings, rather then Vi settings
filetype off                                " This is required for Vundle
syntax enable                               " Activate Syntax Highlight
set encoding=utf-8                          " Set default encoding to UTF-8
set hlsearch                                " Highlight search results
set nowrap                                  " Deactivate Wrapping
set nrformats=                              " Treat all numbers as decimal
set cursorline                              " Highlight the Current Line
set noswapfile                              " I don't like Swapfiles
set shell=bash                              " This makes RVM work inside Vim. I have no idea why – Gary Bernhardt
set timeout timeoutlen=1000 ttimeoutlen=100 " Fix slow O inserts from Gary Bernhardt
set ai                                      " Automatically set the indent of a new line
set spell spelllang=en_us                   " Activate spell checking and set American as default
set list listchars=tab:›\ ,trail:‹          " Show invisible characters: Tabs & trailing whitespace
set expandtab                               " Use Soft-Tabs
set tabstop=2 softtabstop=2 shiftwidth=2    " Use two spaces for tabs, soft-tabs and when shifting
set nojoinspaces                            " Insert only one space when joining lines
match ErrorMsg '\%>100v.\+'                 " Highlight Characters that overstep 100 characters

" Jump between pairs of keywords via %
runtime macros/matchit.vim

" === MacVim Setup ===
if has("gui_running")
  set guifont=Source\ Code\ Pro:h14 " Right Font and Font-Size
  set guioptions=egmrt              " No Toolbar
  set guioptions-=r                 " No Scrollbar
endif

" === Vundle Setup ===
set rtp+=~/.vim/bundle/vundle/
call vundle#rc()
Bundle 'gmarik/vundle'

" =========== Solarized Dark ===========
" Currently using the dark version
Bundle 'altercation/vim-colors-solarized'
set background=dark
colorscheme solarized

" ======== Added Functionality ========
" Polyglot: A collection of language packs, loaded on demand
Bundle "sheerun/vim-polyglot"
" Tabular: Amazing Aligning Utility
Bundle "godlygeek/tabular"
" Fugitive: The best Git integration that exists
Bundle "tpope/vim-fugitive"
" Commentary: Minimalistic commenting plugin
Bundle "tpope/vim-commentary"
" Dispatch: asynchronous build and test dispatcher
Bundle "tpope/vim-dispatch"
" Surround: Quoting/Parenthesizing made simple
Bundle "tpope/vim-surround"
" Repeat: Enable repeat for Surround and Commentary
Bundle "tpope/vim-repeat"
" Emoji: Emojis. For Vim. Awesome.
Bundle "junegunn/vim-emoji"
" Textobj Rubyblock: Custom text object for selecting ruby blocks
Bundle "kana/vim-textobj-user"
Bundle "nelstrom/vim-textobj-rubyblock"
" Rainbow: Keep your sanity in a jungle of parentheses (Only active on Lisps)
Bundle "oblitum/rainbow"
autocmd! FileType lisp,scheme,clojure call rainbow#load()
" Paredit: Port of emacs' paredit to Vim (Only active on Lisps)
Bundle "paredit.vim"

" ======= Syntastic =======
" Really great Syntax checker
Bundle "scrooloose/syntastic"

" Also run a Syntax Check when the file is opened
let g:syntastic_check_on_open=1

" GUI MacVim only: Popover for each line with errors
let g:syntastic_enable_balloons=1

if emoji#available()
  " Terminal Vim for example
  " Bombs and Marks
  " let g:syntastic_error_symbol=emoji#for('boom')
  " let g:syntastic_warning_symbol=emoji#for('exclamation')
  " Cats
  let g:syntastic_error_symbol=emoji#for('scream_cat')
  let g:syntastic_warning_symbol=emoji#for('crying_cat_face')
else
  " GUI MacVim for example
  let g:syntastic_error_symbol='✗'
  let g:syntastic_warning_symbol='⚠'
end

" ======= CtrlP =======
" Full path fuzzy finder
Bundle "kien/ctrlp.vim"

" Ignore files that are git ignored (thanks @railsbros-dirk)
let g:ctrlp_user_command = {
  \ 'types': {
    \ 1: ['.git', 'cd %s && git ls-files --exclude-standard -co'],
    \ 2: ['.ctrlp_root_dir', 'find %s -type f'],
  \ },
  \ 'fallback': 'find %s -type f'
\ }

" Jump to the window when it is already open (only when in this tab)
let g:ctrlp_switch_buffer = 'e'

" Disable caching
let g:ctrlp_use_caching = 0

" === Language Specific Setting ===

" Makefiles require tabs
autocmd! FileType make setlocal noexpandtab

" Make sure all markdown files have the correct filetype
autocmd! BufRead,BufNewFile *.{md,txt} set ft=markdown

" Autogroup for prose file types
augroup prose
  autocmd!

  " Use four spaces for tabs, soft-tabs and when shifting
  autocmd FileType markdown set tabstop=4 softtabstop=4 shiftwidth=4

  " Wrap text
  autocmd FileType tex,markdown set wrap linebreak nolist

  " No character limit per line
  autocmd FileType tex,markdown match ErrorMsg ''
augroup END

" === Custom Functions and Commands ===

" Open the current file in Marked
function! s:marked()
  silent exe "!open -a Marked %"
  redraw!
endfunction
command! Marked :call s:marked()

" Check current file with JSLint
command! JSLint :Dispatch jslint "%:p"

" Check current file with JSHint
command! JSHint :Dispatch jshint "%:p"

" Switch spell checking language
command! British :set spelllang=en_gb
command! American :set spelllang=en_us
command! German :set spelllang=de

" === Mappings ===

" Clear the search buffer when hitting return
nnoremap <CR> :nohlsearch<cr>
" Directory of current file (`:e %%` for example) – Gary Bernhardt
cnoremap %% <C-R>=expand('%:h').'/'<cr>

" === Leader Key ===

" Execute dispatcher's Make
nnoremap <Leader>m :Make<CR>

" Edit my vimrc (from: Learn Vim Script the hard way)
nnoremap <leader>ev :vne $MYVIMRC<cr>

" Source my vimrc (from: Learn Vim Script the hard way)
nnoremap <leader>sv :source $MYVIMRC<cr>

" --- End ---
" This is required for Vundle
filetype plugin indent on
